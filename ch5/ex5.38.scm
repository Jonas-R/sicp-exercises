(load "ch5-compiler.scm")

(define (compile exp target linkage)
  (cond ((self-evaluating? exp)
         (compile-self-evaluating exp target linkage))
        ((quoted? exp) (compile-quoted exp target linkage))
        ((variable? exp)
         (compile-variable exp target linkage))
        ((assignment? exp)
         (compile-assignment exp target linkage))
        ((definition? exp)
         (compile-definition exp target linkage))
        ((if? exp) (compile-if exp target linkage))
        ((lambda? exp) (compile-lambda exp target linkage))
        ((begin? exp)
         (compile-sequence (begin-actions exp)
                           target
                           linkage))
        ((cond? exp) (compile (cond->if exp) target linkage))
	((memq (car exp) '(+ - * / =))
	 (compile-primitive exp target linkage))
        ((application? exp)
         (compile-application exp target linkage))
        (else
         (error "Unknown expression type -- COMPILE" exp))))


(define (spread-arguments oplist)
  (let ((op1 (compile (car oplist) 'arg1 'next))
	(op2 (compile (cadr oplist) 'arg2 'next)))
    (list op1 op2)))

(define (compile-primitive exp target linkage)
  (let ((argument-instructions (spread-arguments (cdr exp)))
	(operation (car exp)))
    (end-with-linkage linkage
		      (preserving '(env)
		       (car argument-instructions)
		       (preserving '(arg1)
				   (cadr argument-instructions)
				   (make-instruction-sequence '(arg1 arg2) (list target)
							      `((assign ,target (op ,operation) (reg arg1) (reg arg2)))))))))
